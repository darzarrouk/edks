!	include 'modules.inc'

	program tab4E
	
!	use f90_unix 	!needed on the NAG compiler
	use model
	use wavenumber
	use parameters
	use constants
	implicit none

	! local variables
	character (len=256)	:: output_filename
	integer, parameter	:: nkmin=100
	real, dimension(0:2)	:: Pp, Pm, SVp, SVm, SHp, SHm
	real			:: Ep, Em
	real, allocatable	:: q(:,:), w(:,:), v(:,:), qE(:), wE(:)

	real			:: B(0:2), C(0:2), D(3:4), E, F

	real, allocatable	:: rv(:)
	integer			:: jh, jk, jr, nr
	real			:: r, rmin, rmax, dr
	real, allocatable	:: Bmat(:,:), Cmat(:,:), Dmat(:,:), Emat(:), Fmat(:)
	real			:: hmin, hmax

	real			:: depth
	
	integer			:: narg
 	integer			:: iargc
	character (len=256)	:: argum

!	read the command line arguments
	narg = iargc()
	if (narg .ne. 5) then
		write(*,'(a)') 'Syntax:  tab4E model_filename depth(km)  dr(km) rmax(km) output_filename'
		call exit(1)
	endif


	call getarg(1, argum)
	read(argum, '(a)',err=100) model_filename
	call getarg(2, argum)
	read(argum, *, err=100) depth			! depth of the source
	depth = depth * 1000.
	call getarg(3, argum)
	read(argum, *, err=100) dr			! horizontal interval
	dr = dr * 1000.
	call getarg(4, argum)
	read(argum, *, err=100) rmax			! max. horiz. distance
	rmax = rmax * 1000.
	call getarg(5, argum)
	read(argum, *, err=100) output_filename	
	
	! read the factors defining the k-integration upper limit and step
	open(unit=8, file='c_tab4', status='old', form='formatted')
	read(8, *) facmin
	read(8, *) facmax
	close(8)

	! read the model  (vp, vs, rho, thickness)
	call read_model(model_filename)

	! generate the horizontal distances vector
	rmin = 0.
	nr	= int((rmax-rmin)/dr+1.5)
	allocate(rv(nr), Bmat(0:2,nr), Cmat(0:2,nr), Dmat(3:4,nr), Emat(nr), Fmat(nr))
	rv 	= (/ ( rmin+jr*dr, jr=0,(nr-1) ) /)

	! write the header of the output table
	open(unit=10, file=output_filename, status='unknown')
	write(10,'(a)') 'Model: 1st Nb layers, 2nd th, 3th vp, 4th vs, 5th rho'
	write(10,'(i4)')	N-1
	write(10,'(10f12.2)') 	th0
	write(10,'(10f12.2)') 	vp0
	write(10,'(10f12.2)') 	vs0
	write(10,'(10f12.2)') 	rh0
	write(10,'(a)') 'Depth'
	write(10,'(10f12.2)') 	depth
	write(10,'(a)') 'Distances: nr, rmin, rmax'
	write(10,'(i10, 2f15.2)') nr, rmin, rmax
	write(10,'(a)') 'k-integration parameters'
	write(10,'(2f12.2)') facmin, facmax

	! 'acotab' insert in the model a dummy interface at the depth of the source
	htol = 9
	call acotab(depth, htol)

	! 'discotab' evaluates the discontinuities in the potentials, generated by the source
	call discotabE(M, Pp, Pm, SVp, SVm, SHp, SHm, Ep, Em)

	! calculation for distance r=0
	jr 	= 1
	hmin	= th(1)
	do jh = 2,Np
		hmin = min(hmin,th(jh))
	enddo

	kmax	= facmax/hmin
	hmax	= sum(th)
	dk	= 1./facmin/hmax
	nk	= kmax/dk
	if( nk < nkmin) nk = nkmin
        allocate(kv(nk))
	kmin = dk/100.
        kv    = (/ ( kmin+jk*dk, jk=0,(nk-1) ) /)

	! calculation of the potentials at the surface
	allocate(q(0:2,nk), w(0:2,nk), v(0:2,nk), qE(nk), wE(nk))
	call calcqwvE(Pp, Pm, SVp, SVm, SHp, SHm, Ep, Em, q, w, v, qE, wE)

	! calculation of the elementary displacements at the origin
	call calc_BCD0E(q,w,v,qE, wE, B, C, D, E, F)
	Bmat(:,jr) = B*dk
	Cmat(:,jr) = C*dk
	Dmat(:,jr) = D*dk
	Emat(  jr) = E*dk
	Fmat(  jr) = F*dk
	deallocate(q,w,v,qE,wE)
	deallocate(kv)

	do jr=2,nr
		r = rv(jr)
		kmax	= facmax/hmin
		if(r < hmin) kmax = facmax/r
		dk	= 1./facmin/hmax
		if(r > hmax) dk = 1./facmin/r
		nk	= kmax/dk
		if( nk < nkmin) nk = nkmin
!		write(*,'(a, f9.2, a, e9.4, a, e9.4, a, i5)') ' r= ', r, ' dk= ', dk, ' kmax= ', kmax, ' nk= ', nk
        	allocate(kv(nk))
		kmin = dk/100.
        	kv    = (/ ( kmin+jk*dk, jk=0,(nk-1) ) /)

		! calculation of the potentials at the surface
		allocate(q(0:2,nk), w(0:2,nk), v(0:2,nk), qE(nk), wE(nk))
		call calcqwvE(Pp, Pm, SVp, SVm, SHp, SHm, Ep, Em, q, w, v, qE, wE)

		! calculation of the elementary displacements at the distance r
		call calc_coeffsE(q, w, v, qE, wE, r, B, C, D, E, F)
		Bmat(:,jr) = B*dk
		Cmat(:,jr) = C*dk
		Dmat(:,jr) = D*dk
		Emat(  jr) = E*dk
		Fmat(  jr) = F*dk
		deallocate(q,w,v,qE,wE)
		deallocate(kv)

!		write(*,'(9e15.7)') rv(jr), Bm(:,jr), Cm(:,jr), Dm(:,jr)
		write(*,'(t1,f4.1,"%")') 100.*float(jr-1)/float(nr)
	enddo

	Bmat = Bmat/4./M_PI
	Cmat = Cmat/4./M_PI
	Dmat = Dmat/4./M_PI
	Emat = Emat/4./M_PI
	Fmat = Fmat/4./M_PI
	write(10,'(a)') 'elementary displacements'
	do jr=1,nr
		write(10,'(12e15.7)') depth, rv(jr), Bmat(:,jr), Cmat(:,jr), Dmat(:,jr), Emat(jr), Fmat(jr)
	enddo
	close(10)

	deallocate(vp,  vs,  rh,  th)
	deallocate(lambda, mu, delta, gamma)
	deallocate(rv, Bmat, Cmat, Dmat, Emat, Fmat)
	deallocate(vp0, vs0, rh0, th0)

	stop
 100	write(*,'(a)') '!!! Error decoding the command-line parameters'
	write(*,'(a)') '!!! Syntax:  tab4 model_filename depth rmax dr output_filename'
	stop

	end program tab4E
